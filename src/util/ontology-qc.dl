#define RDF_TYPE "<http://www.w3.org/1999/02/22-rdf-syntax-ns#type>"
#define RDFS_SUBCLASS_OF "<http://www.w3.org/2000/01/rdf-schema#subClassOf>"
#define RDFS_SUBPROPERTY_OF "<http://www.w3.org/2000/01/rdf-schema#subPropertyOf>"
#define OWL_CLASS "<http://www.w3.org/2002/07/owl#Class>"
#define OWL_DEPRECATED "<http://www.w3.org/2002/07/owl#deprecated>"
#define OWL_ANNOTATION_PROPERTY "<http://www.w3.org/2002/07/owl#AnnotationProperty>"
#define OWL_AXIOM "<http://www.w3.org/2002/07/owl#Axiom>"
#define OWL_ANNOTATED_SOURCE "<http://www.w3.org/2002/07/owl#annotatedSource>"
#define OWL_ANNOTATED_PROPERTY "<http://www.w3.org/2002/07/owl#annotatedProperty>"
#define OWL_TRANSITIVE_PROPERTY "<http://www.w3.org/2002/07/owl#TransitiveProperty>"
#define OIO_HAS_DB_XREF "<http://www.geneontology.org/formats/oboInOwl#hasDbXref>"
#define OIO_HAS_EXACT_SYNONYM "<http://www.geneontology.org/formats/oboInOwl#hasExactSynonym>"
#define OIO_HAS_RELATED_SYNONYM "<http://www.geneontology.org/formats/oboInOwl#hasRelatedSynonym>"
#define OIO_HAS_NARROW_SYNONYM "<http://www.geneontology.org/formats/oboInOwl#hasNarrowSynonym>"
#define OIO_HAS_BROAD_SYNONYM "<http://www.geneontology.org/formats/oboInOwl#hasBroadSynonym>"
#define OIO_SUBSET_PROPERTY "<http://www.geneontology.org/formats/oboInOwl#SubsetProperty>"
#define OIO_IN_SUBSET "<http://www.geneontology.org/formats/oboInOwl#inSubset>"
#define IAO_DEFINITION "<http://purl.obolibrary.org/obo/IAO_0000115>"

.decl error(message: symbol)
.decl ontrdf(s: symbol, p: symbol, o: symbol)
.decl term(iri: symbol)
.decl obsolete_term(iri: symbol)
.decl valid_term(iri: symbol)
.decl synonym_property(iri: symbol)
.decl declared_subset(iri: symbol)
.decl subset_assertion(term: symbol, subset: symbol)
.decl xref(term: symbol, ref: symbol)
.decl defxref(term: symbol, ref: symbol)
.decl synonym_xref(term: symbol, property: symbol, ref: symbol)
.decl obsolete_ec(ec: symbol)

.input ontrdf(filename="go-edit.facts")
.input obsolete_ec(filename="../resources/obsolete_ec.txt")

synonym_property(OIO_HAS_EXACT_SYNONYM).
synonym_property(OIO_HAS_RELATED_SYNONYM).
synonym_property(OIO_HAS_NARROW_SYNONYM).
synonym_property(OIO_HAS_BROAD_SYNONYM).

term(iri) :- ontrdf(iri, RDF_TYPE, OWL_CLASS), match("<.+>", iri).

obsolete_term(iri) :- term(iri), ontrdf(iri, OWL_DEPRECATED, "\"true\"^^<http://www.w3.org/2001/XMLSchema#boolean>").

valid_term(iri) :- term(iri), !obsolete_term(iri).

declared_subset(iri) :- ontrdf(iri, RDF_TYPE, OWL_ANNOTATION_PROPERTY), ontrdf(iri, RDFS_SUBPROPERTY_OF, OIO_SUBSET_PROPERTY).
subset_assertion(term, subset) :- ontrdf(term, OIO_IN_SUBSET, subset).

xref(term, ref) :- ontrdf(term, OIO_HAS_DB_XREF, ref), term(term).

defxref(term, ref) :- 
    ontrdf(axiom, OWL_ANNOTATED_SOURCE, term),
    term(term),
    ontrdf(axiom, OWL_ANNOTATED_PROPERTY, IAO_DEFINITION),
    ontrdf(axiom, OIO_HAS_DB_XREF, ref).

synonym_xref(term, property, ref) :-
    ontrdf(axiom, OWL_ANNOTATED_SOURCE, term),
    term(term),
    ontrdf(axiom, OWL_ANNOTATED_PROPERTY, property),
    synonym_property(property),
    ontrdf(axiom, OIO_HAS_DB_XREF, ref).

error(cat("ERROR: ", term, " references an undeclared subset: ", subset)) :- subset_assertion(term, subset), !declared_subset(subset).

.output error(filename="datalog-violations.tsv")
