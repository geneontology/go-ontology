PREFIX rh: <http://rdf.rhea-db.org/>
PREFIX ch: <http://purl.obolibrary.org/obo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX gorhea: <http://purl.obolibrary.org/obo/go/rhea/>

CONSTRUCT {
    ?reaction rdfs:subClassOf rh:Reaction . 
    ?reaction rdfs:label ?reaction_label .
    ?reaction rh:ec ?ec .
    ?reaction rh:side ?reaction_side .
    ?reaction_side rh:curatedOrder ?side_order .
    ?reaction_side rh:contains ?participant .
    ?participant rh:coefficient ?stoichiometry . 
    ?participant rh:chebi ?chebi .
    ?participant rh:compound ?compound .
    ?compound gorhea:compound_label ?compound_label .
    ?participant rdfs:subClassOf ?compound_type .
}
WHERE {
VALUES ?reaction_type { rh:Reaction rh:DirectionalReaction rh:BidirectionalReaction }
?reaction rdfs:subClassOf ?reaction_type .
?reaction rh:status rh:Approved .
?reaction rh:equation ?reaction_label . 
OPTIONAL {
    ?reaction rh:ec ?ec .
}
# get participants
?reaction rh:side|rh:substrates|rh:products|rh:substratesOrProducts ?reaction_side .
?reaction_side rh:curatedOrder ?side_order .
?reaction_side rh:contains ?participant .
?reaction_side ?containsWithStoich ?participant . 
?containsWithStoich rh:coefficient ?stoichiometry . 
?participant rh:compound ?compound .
?compound rh:name ?compound_label .  
# get chebi for participants
?compound rh:chebi|rh:underlyingChebi|(rh:reactivePart/rh:chebi) ?chebi .
?compound rdfs:subClassOf ?compound_type .
# This list is vulnerable to getting out of sync with Rhea
# They are running Virtuoso and it doesn't seem to support property paths
VALUES ?compound_type { rh:SmallMolecule rh:GenericCompound rh:Polymer rh:GenericHeteropolysaccharide rh:GenericPolynucleotide rh:GenericPolypeptide rh:GenericSmallMolecule }
}
