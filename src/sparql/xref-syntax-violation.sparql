PREFIX oio: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?c ?xref WHERE 
{
  {
    ?c oio:hasDbXref ?xref .
  }
  UNION
  {
    ?axiom a owl:Axiom ;
        owl:annotatedSource ?c ;
        owl:annotatedProperty ?p ;
        owl:annotatedTarget ?o ;
        oio:hasDbXref ?xref .
  }
  FILTER(
    !CONTAINS(STR(?xref), ":") || 
    CONTAINS(STR(?xref), " ") || 
    STR(?xref) = "" ||
    ((STRBEFORE(STR(?xref), ":") = "RHEA") && !REGEX(STRAFTER(STR(?xref), ":"), "\\d+")) ||
    ((STRBEFORE(STR(?xref), ":") = "EC") && !REGEX(STRAFTER(STR(?xref), ":"), "(^\\d+\\.\\d+\\.\\d+\\.[a-zA-Z\\d]+$)|(^\\d+\\.\\d+\\.\\d+\\.-$)|(^\\d+\\.\\d+\\.-\\.-$)|(^\\d+\\.-\\.-\\.-$)"))
  )
}
